
set key left top
set term png

unset yrange

# -----------
set out 'dj-both-dj-ctr.png'
set title "Num connectors vs Num disjuncts"
set ylabel "Number of Connectors"
set logscale x
set logscale y
set xlabel "Number of Disjuncts (#dj)"
plot "pandp.dat" using 5:7 with points title "Pride & Prej", \
     (1 * (x)**1.17) with lines lw 2 title "#dj^{1.17}", \
     "djc.dat" using 3:5 with points title "third dataset", \
     (4 * x) with lines lw 2 title "4 * #dj"

unset xrange
unset yrange

# -----------
set out 'dj-both-dj-cnt.png'
set title "Alloced Table connectors vs Num disjuncts"
set ylabel "Number of Table connector"
set logscale x
set logscale y
set xrange [1000:]
set yrange [10000:2e6]

set xlabel "Number of Disjuncts (#dj)"
plot "pandp.dat" using 5:11 with points title "Pride & Prej", \
     (0.1 * (x)**1.5) with lines lw 2 title "0.1 #dj^{1.5}", \
     "djc.dat" using 3:7 with points title "third dataset", \
     (0.7 * (x**1.2)) with lines lw 2 title "0.7 * #dj^{1.2}"

unset xrange
unset yrange

# -----------
set out 'dj-both-dj-cnt-act.png'
set title "Used Table connectors vs Num disjuncts"
set ylabel "Number of Table connector"
set logscale x
set logscale y
set xrange [3:5e4]
set yrange [5:2e6]

set xlabel "Number of Disjuncts (#dj)"
plot "pandp-more.dat" using 5:41 with points title "Pride & Prej", \
     (0.1 * (x)**1.5) with lines lw 2 title "0.1 #dj^{1.5}", \
     "djc.dat" using 3:7 with points title "third dataset", \
     (0.7 * (x**1.2)) with lines lw 2 title "0.7 * #dj^{1.2}"

unset xrange
unset yrange

# -----------
set out 'dj-both-dj-buck.png'
set title "Alloced Buckets vs Num disjuncts"
set ylabel "Number of Buckets alloced"
set logscale x
set logscale y
set xlabel "Number of Disjuncts (#dj)"
set xrange [1000:]
set yrange [1000:2e6]
plot "pandp.dat" using 5:13 with points title "Pride & Prej",\
     (0.1 * x**1.17) with lines lw 2 title "0.1 #dj^{1.17}", \
     "djc.dat" using 3:9 with points title "third dataset", \
     (3e-2 * x**1.5) with lines lw 2 title "0.03 #dj^{1.5}"

unset xrange
unset yrange

# -----------
set out 'dj-both-buck-act.png'
set title "Used Pset Buckets vs Num disjuncts"
set ylabel "Number of Pset Buckets used"
set logscale x
set logscale y
set xlabel "Number of Dissjuncts"
set xrange [3:2e4]
set yrange [10:]
# expressions are column 31
# words are column 3
# disjuncts are column 5
f(x) = 1.0/sqrt(x)
g(x) = 2**floor (1.5 * log(x) / 0.6931)
max(x1,x2) = (x1>x2)? x1 : x2
h(x) = 2**floor (max ((1.5 * log(x) / 0.6931), (1 + log(8*x) / 0.6931)))
plot "pandp-full.dat" using (f($3) * $5):37 with points title "Pride Prejudice", \
     "dj-150-full.dat" using (f($3) * $5):37 with points title "dj-150 MST", \
     0.5*h(x) with lines lw 2 title "0.5 x-table-size"

###     "pandp-full.dat" using (f($3) * $5):(0.2*$31) with points title "Pride Prejudice", \
###     "dj-150-full.dat" using (f($3) * $5):(0.2*$31) with points title "dj-150 MST", \
unset xrange
unset yrange

# -----------

set out 'dj-both-dj-cho.png'
set title "Alloced Parse choices vs Num disjuncts"
set ylabel "Number of Parse choice alloced"
set logscale x
set logscale y
set xrange [400:]
set yrange [1e3:]
set xlabel "Number of Disjuncts (#dj)"
plot "pandp.dat" using 5:15 with points title "Pride & Prej", \
     (0.01 * x**1.5) with lines lw 2 title "0.01 #dj^{1.5}", \
     "djl.dat" using 3:9 with points title "second dataset",\
     ((0.16 * x)**2) with lines lw 2 title "(0.16 #dj)^2"

# -----------

set out 'dj-both-dj-cho-act.png'
set title "Used Parse choices vs Number of disjuncts"
set ylabel "Number of Parse choice used"
set logscale x
set logscale y
set xrange [2:]
set yrange [2:]
set xlabel "Number of Disjuncts"
plot "pandp-more.dat" using 5:39 with points title "Pride Prejudice", \
     "djl.dat" using 3:9 with points title "second MST dataset",\
     "dj-1500.dat" using 3:11 with points title "MST link-lim=1500 dataset",\
     "dj-150.dat" using 5:15 with points title "MST link-lim=150 dataset",\
     (16 * x) with lines lw 2 title "16 #dj"

# -----------

set out 'dj-both-dj-cho-scaled-act.png'
set title "Used Parse choices vs (Num dj / num words squared)"
set ylabel "Number of Parse choice used"
set logscale x
set logscale y
set xrange [0.1:]
set yrange [1:]
set xlabel "Number of Disjuncts / num words **2"
#f(x) = 1
#f(x) = 1/x
#f(x) = 1/(x*log(x))
#f(x) = 1/(x*sqrt(x))
f(x) = 1/(x*x)
plot "pandp-more.dat" using (f($3) * $5):39 with points title "Pride Prejudice", \
     "djl.dat" using (f(10.0) * $3):9 with points title "second MST dataset",\
     "dj-1500.dat" using (f(10.0) * $3):11 with points title "MST link-lim=1500 dataset",\
     "dj-150.dat" using (f($3) * $5):15 with points title "MST link-lim=150 dataset",\
     ((16 * x)**2) with lines lw 2 title "(16 #dj)^2"

unset xrange
unset yrange
# -----------

set out 'dj-both-time.png'
set title "Parse Time vs Num Disjuncts"
set ylabel "Parse Time (secs)"
set logscale x
set logscale y
set xrange [20:]
set yrange [2e-3:300]
set xlabel "Number of Disjuncts"
plot "pandp-full.dat" using 5:17 with points title "Pride Prejudice parse", \
     "pandp-full.dat" using 5:19 with points title "Pride Prejudice extract", \
     "dj-150-full.dat" using 5:17 with points title "MST link-lim=150 parse",\
     "dj-150-full.dat" using 5:19 with points title "MST link-lim=150 extract",\
     "dj-1500-full.dat" using 5:17 with points title "MST link-lim=1500 parse",\
     "dj-1500-full.dat" using 5:19 with points title "MST link-lim=1500 extract",\
     (7e-9 * x**2) with lines lw 2 title "7e-9 (#dj)^2"

unset xrange
unset yrange
# -----------

set out 'dj-both-ratio.png'
set title "Ratio Time to Extract / Parse vs Num Disjuncts"
set ylabel "Extract / Parse Time Ratio"
set logscale x
set logscale y
set key left bottom
set xrange [100:]
set yrange [0.2:10]
set xlabel "Number of Disjuncts"
plot "pandp-full.dat" using 5:($19 / $17) with points title "Pride Prejudice", \
     "dj-150-full.dat" using 5:($19 / $17) with points title "MST link-lim=150",\
     "dj-1500-full.dat" using 5:($19 / $17) with points title "MST link-lim=1500"

#     "dj-15000-full.dat" using 5:($19 / $17) with points title "MST link-lim=15000"

unset xrange
unset yrange
set key left top
# -----------

set out 'time-both-ratio.png'
set title "Ratio Time to Extract / Parse vs Time to Parse"
set ylabel "Extract / Parse Time Ratio"
set logscale x
set logscale y
set key right bottom
set xrange [1e-3:]
set yrange [0.2:10]
set xlabel "Time to Parse (seconds)"
plot "pandp-full.dat" using 17:($19 / $17) with points title "Pride Prejudice", \
     "dj-150-full.dat" using 17:($19 / $17) with points title "MST link-lim=150",\
     "dj-1500-full.dat" using 17:($19 / $17) with points title "MST link-lim=1500", \
     "dj-15000-full.dat" using 17:($19 / $17) with points title "MST link-lim=15000"

unset xrange
unset yrange
set key left top
# -----------

set out 'pc-both-time.png'
set title "Time to Extract vs Num Parse choice"
set ylabel "Extract Time (seconds)"
set logscale x
set logscale y
set xrange [10:]
set yrange [1e-3:]
set xlabel "Number of Parse choice"
plot "pandp-full.dat" using 39:19 with points title "Pride Prejudice", \
     "dj-150-full.dat" using 39:19 with points title "MST link-lim=150",\
     "dj-1500-full.dat" using 39:19 with points title "MST link-lim=1500", \
		1e-6 * x with lines lw 2 title "1e-6 x"

unset xrange
unset yrange
# -----------

set out 'exp-both-cho-act.png'
set title "Used Parse choices vs Number of expressions"
set ylabel "Number of Parse choice used"
set logscale x
set logscale y
set xrange [300: 5e6]
set yrange [2:]
set xlabel "Number of Expressions"
plot "pandp-full.dat" using 31:39 with points title "Pride Prejudice", \
     "dj-150-full.dat" using 31:39 with points title "dj-150 MST", \
     1e-5 * x**2  with lines lw 2 title "1e-5 (num exp)^2"

unset xrange
unset yrange

# -----------
set out 'exp-both-buck-act.png'
set title "Used Pset Buckets vs Num expressions"
set ylabel "Number of Pset Buckets used"
set logscale x
set logscale y
set xlabel "Number of Expressions"
set xrange [300: 5e6]
set yrange [2:]
# expressions are column 31
# words are column 3
# disjuncts are column 5
plot "pandp-full.dat" using 31:37 with points title "Pride Prejudice", \
     "dj-150-full.dat" using 31:37 with points title "dj-150 MST", \
     1e-5 * x**1.5  with lines lw 2 title "1e-5 (num exp)^{3/2}", \
      0.2*x  with lines lw 2 title "0.2 x"

unset xrange
unset yrange

# -----------
set out 'exp-both-tracon-act.png'
set title "Tracon elts vs Num expressions"
set ylabel "Number of Traceon Pool elts used"
set logscale x
set logscale y
set xlabel "Number of Expressions"
set xrange [300:]
set yrange [2:]
f(w)=1/w
f(w)=1/sqrt(w)
plot "pandp-full.dat" using (f($3) * $31):41 with points title "Pride Prejudice", \
     "dj-150-full.dat" using (f($3) * $31):41 with points title "dj-150 MST", \
     1e-5 * x**2  with lines lw 2 title "1e-5 (num exp)^2"

unset xrange
unset yrange

# -----------

set out 'exp-both-time.png'
set title "Parse Time vs Num Expressions"
set ylabel "Parse Time (secs)"
set logscale x
set logscale y
set xrange [1000:]
set yrange [2e-3:300]
set xlabel "Number of Expressions"
plot "pandp-full.dat" using 31:17 with points title "Pride Prejudice parse", \
     "pandp-full.dat" using 31:19 with points title "Pride Prejudice extract", \
     "dj-150-full.dat" using 31:17 with points title "MST link-lim=150 parse",\
     "dj-150-full.dat" using 31:19 with points title "MST link-lim=150 extract",\
     (1e-8 * x**1.5) with lines lw 2 title "1e-8 (ex)^{3/2}"

unset xrange
unset yrange
# -----------
