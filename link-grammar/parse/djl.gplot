
set key left top
set term png

# -----------
set out 'djl-e-dj.png'
set title "Num disjuncts vs Sequence"
set xlabel "Sequence"
set ylabel "Num Disjuncts"
set logscale y
plot "djl.dat" using 1:3 with points title ""

# -----------
set out 'djl-e-chob.png'
set title "Alloced Parse choice Bytes vs Sequence"
set ylabel "Bytes alloc'ed Parse choice"
plot "djl.dat" using 1:(96 * $9) with points title ""

# -----------
set out 'djl-dj-cho.png'
set title "Alloced Choices vs Num disjuncts"
set ylabel "Number of Parse choice alloced"
set logscale x
set logscale y
set xrange [100:]
set yrange [200:]
set xlabel "Number of Disjuncts (#dj)"
plot "djl.dat" using 3:9 with points title "second dataset",\
     "dj.dat" using 2:8 with points title "first dataset",\
     "dj-1500.dat" using 3:9 with points title "linkage-lim=1500 dataset",\
     "pandp.dat" using 5:15 with points title "Pride & Prej", \
     ((0.16 * x)**2) with lines lw 2 title "(0.16 * #dj)^2", \
     ((0.1 * x)**1.5) with lines lw 2 title "(0.1 * #dj)^{1.5}"

unset xrange
unset yrange

# -----------
set out 'djl-dj-cho-scaled.png'
set title "Alloced Choices vs (Disjuncts x Linakges)"
set ylabel "Number of Parse choice alloced"
set logscale x
set logscale y
set xrange [2e5:]
set yrange [500:1e9]
set xlabel "(Num Disjuncts) x (Linkage Limit)"
plot "djl.dat" using (15000 * $3):9 with points title "second MST dataset",\
     "dj.dat" using (15000 * $2):8 with points title "first MST dataset",\
     "dj-1500.dat" using (1500 * $3):9 with points title "linkage-lim=1500 MST dataset",\
     "pandp.dat" using (1000 * $5):15 with points title "Pride Prejude; linkage-lim=1K", \
     ((0.000016 * x)**2) with lines lw 2 title "(0.00016 * #dj)^2", \
     ((0.0001 * x)**1.5) with lines lw 2 title "(0.0001 * #dj)^{1.5}"

unset xrange
unset yrange

# -----------
set out 'djl-dj-buck.png'
set title "Alloced Buckets vs Num disjuncts"
set ylabel "Number of Buckets alloced"
set logscale x
set logscale y
set xlabel "Number of Disjuncts (#dj)"
plot "djl.dat" using 3:7 with points title "second dataset",\
     "dj.dat" using 2:6 with points title "first dataset",\
     (3e-2 * x**1.5) with lines title "#dj^{1.5}"


# -----------
set out 'djl-dj.png'
set title "Parse time vs Disjuncts"
set ylabel "Total Parse Time (seconds)"
set logscale x
set logscale y
set xlabel "Number of Disjuncts"
plot "djl.dat" using 3:13 with points title "second dataset", \
     "dj.dat" using 2:12 with points title "first dataset", \
     ((1.5e-4 * x)**2) with lines title "square of #dj"

# -----------
set out 'djl-tm-chob.png'
set title "Alloced Parse choice Bytes vs Parse Time"
set ylabel "Bytes alloc'ed Parse choice"
set logscale x
set logscale y
set xlabel "Parse Time (seconds)"
plot "djl.dat" using 13:(96 * $9) with points title "second dataset", \
     "dj.dat" using 12:(96 * $8) with points title "first dataset", \
     (96 * 2e6* x) with lines title "linear"

