%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                               %
%       Thai Link Grammar                                       %
%                                                               %
%       Copyright (C) 2021  Prachya Boonkwan                    %
%       National Electronics and Computer Technology Center     %
%       Thailand                                                %
%                                                               %
%       License: Creative Commons, Attribution (CC-BY)          %
%                                                               %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

#define dictionary-version-number 0.0.1;
#define dictionary-locale th_TH.UTF-8;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   DEPENDENCY LINKS
%
%   LW = left wall
%       LWs = sentence
%       LWn = utterance of noun phrase
%       LWp = utterance of preposition phrase
%   RW = right wall
%   VS = serial verb
%   NS = serial noun
%   S  = subject
%   O  = object
%   AJ = adjective
%       AJr = relative clause
%       AJj = simple adjective
%       AJv = attributive verb
%       AJp = preposition phrase
%       AJt = temporal modifier
%   AX = auxiliary
%       AXw = simple auxiliary
%       AXg = negative auxiliary
%   AV = adverb
%       AVp = preposition phrase
%       AVw = simple adverb
%       AVg = negative adverb
%       AVn = numeral phrase modifying noun phrase
%       AVv = numeral phrase modifying verb phrase
%       AVt = temporal entity modifying verb phrase
%       AVc = clause-level adverb
%   PT = particle
%   IJ = interjection
%   NU = number
%       NUn = simple number
%       NUv = number for verb
%   CL = classifier
%       CLn = classifier for noun
%       CLv = classifier for verb
%   PO = prepositional object (noun)
%   PC = prepositional complement (verb)
%   JN = conjoined noun
%   JV = conjoined verb
%   TP = topicalization
%   RI = implicit relative clause
%   AT = attribute
%   QF = numeral quantifier
%   AM = attribute modifier
%   PS = possessive pronoun
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                       %
%   COMMON MACROS                       %
%                                       %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

<left-wall-sent> :
    {@IJ-}
    & LWs-;

<left-wall-noun> :
    {@IJ-}
    & LWn-;

<left-wall-prep> :
    {@IJ-}
    & LWp-;

<right-wall> :
    {@PT+} 
    & RW+;

<utterance-noun> :
    <left-wall-noun>
    & <right-wall>;

<utterance-prep> :
    <left-wall-prep>
    & <right-wall>;

<num-cl> :
    hNUn- or hNUn+;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                       %
%   GRAMMAR                             %
%                                       %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%
% Noun
%%%%%%%%%%%%%%%%%%%%

<noun-looking-for-mods> :
    {@hAJ+ or hPS+}
    & {PUp+ or PUy+}
    & {<num-cl>}
    & {@hAJ+};

<noun-start> :
    {[NS+] or hAT+}
    & <noun-looking-for-mods>;

<noun-start-temporal> :
    {TS+ or hAT+}
    & <noun-looking-for-mods>
    & {PUsr+ & TS+};

<noun-serial> :
    NS-
    & {NS+};

<noun-serial-temporal> :
    TS-
    & {TS+};

<noun-imprel> :
    hRI+
    & {@hAJ+};

<noun-attribute> :
    dAT-
    & (@hAJ+ or hPO+);

<noun-element> :
    dS+
    or dO-
    or [<utterance-noun>]
    or dPO-
    or JNl+ 
    or JNr- 
    or ({PUst+} & TP+);

<noun-simple> :
    (<noun-start> & <noun-element>)
    or <noun-serial>
    or (<noun-imprel> & <noun-element>);

<noun-temporal> :
    dAVtl+
    or dAVtr-;

%%%% Common Noun %%%%

/th/words/words.n.common 
THAI-COLOR.n :
    <noun-simple>;

%%%% Attributive Noun %%%%

/th/words/words.n.attribute :
    (<noun-start> & <noun-element>)
    or <noun-attribute>;

%%%% Nominalizing Prefix %%%%

/th/words/words.n.prefix :
    <noun-imprel>
    & <noun-element>;

%%%% Temporal Entity %%%%

THAI-DATE THAI-TIME :
    <noun-start-temporal>
    & (<noun-temporal> or <noun-element>);

%%%%%%%%%%%%%%%%%%%%
%   Pronoun
%%%%%%%%%%%%%%%%%%%%

/th/words/words.pro.common :
    (
        {hAJr+}
        & {<num-cl>}
        & <noun-element>
    )
    or dPS-;

%%%%%%%%%%%%%%%%%%%%
% Classifier
%%%%%%%%%%%%%%%%%%%%

<cls-n> :
    dCLn+ or dCLn-;

<cls-v> :
    dCLv+ or dCLv-;

/th/words/words.cl.noun :
    <cls-n>;

/th/words/words.cl.verb :
    <cls-v>;

%%%%%%%%%%%%%%%%%%%%
% Verb
%%%%%%%%%%%%%%%%%%%%

<verb-start> :
    {@hAX-}
    & (
        (hS- & ([TP-] or ()))
        or [()]
    )
    & {hAVpl-}
    & {hAVtl-}
    & {hAVcl-}
    & (<left-wall-sent> or JVr-)
    & (hO+ or [()])
    & (
        ({@hAV+} & <right-wall>)
        or [VS+] 
        or ({@hAV+} & JVl+)
    );

<verb-intrans> :
    {@hAX-}
    & (
        (hS- & ([TP-] or ()))
        or [()]
    )
    & {hAVpl-}
    & {hAVcl-}
    & (<left-wall-sent> or JVr-)
    & (
        ({@hAV+} & {@hAV-} & <right-wall>)
        or [VS+]
        or ({@hAV+} & JVl+)
    );

<verb-serial> :
    VS-
    & (hO+ or [()])
    & {@hAV+}
    & (<right-wall> or VS+);

<verb-comp> :
    {@hAX-}
    & {hS- & ([TP-] or ())}
    & {hAVpl-}
    & {dPC- or dRC- or dZN-}
    & (hO+ or [()])
    & {@hAV+}
    & {[VS+]};

<verb-imprel> :
    [dRI-]
    & (hO+ or [()])
    & {[hAV+]};

<verb-simple> :
    (
        {[[PUsl-]]}
        & {[[PUsr+]]}
        & (<verb-start> or <verb-serial> or <verb-comp>)
    )
    or [<verb-imprel>];

<verb-simple-intrans> :
    {[[PUsl-]]}
    & {[[PUsr+]]}
    & <verb-intrans>;

%%%% Common Verb %%%%

/th/words/words.v.common :
    <verb-simple>;

%%%%%%%%%%%%%%%%%%%%
% Adjective
%%%%%%%%%%%%%%%%%%%%

<adj-simple> :
    {hCL-}
    & dAJj-
    & {hAVw+};

<adj-lhs> :
    dAJj+;

<adj-attribute> :
    {hCL-}
    & dAJv-
    & {hAVw+ or hAVp+};

%%%% Simple Adjective %%%%

/th/words/words.adj.common
/th/words/words.num.ordinal
THAI-COLOR.j :
    <adj-simple>;

/th/words/words.adj.lhs :
    <adj-lhs>;

%%%% Attributive Verb %%%%

/th/words/words.v.attribute
THAI-COLOR.va :
    {hAM-}
    & (
        <adj-attribute>
        or [<verb-simple-intrans> or <verb-comp>]
    );

%%%%%%%%%%%%%%%%%%%%
% Number
%%%%%%%%%%%%%%%%%%%%

<number-simple> :
    ({hQFl-} & dNUnr- & hCLn+ & {hQFr+})
    or [{hQFl-} & dAVnr- & hCLn+ & {hQFr+}]
    or [{hQFl-} & dAVvr- & hCLv+ & {hQFr+}]
    or dNUnl+
    or dNUtr-
    or dONr-;

/th/words/words.num.common
NUMBERS.nu FRACTION.nu 
THAI-NUMBERS.nu THAI-FRACTION.nu:
    {PUsl-} & {PUsr+} & <number-simple>;

/th/words/words.num.movable :
    (hCLn- & (dNUnr- or [dAVnr-]))
    or (hCLv- & (dNUvr- or [dAVvr-]));

/th/words/words.qf.lhs :
    dQFl+;

/th/words/words.qf.rhs :
    dQFr-;

/th/words/words.ordmark :
    hONr+
    & <adj-simple>;

%%%%%%%%%%%%%%%%%%%%
% Auxiliary
%%%%%%%%%%%%%%%%%%%%

/th/words/words.aux.common :
    dAXw+;

%%%%%%%%%%%%%%%%%%%%
% Adverb
%%%%%%%%%%%%%%%%%%%%

/th/words/words.adv.common :
    dAVw- & {hAV+};

/th/words/words.adv.attrmod :
    dAM+ & {hAV+};

/th/words/words.adv.cohesive :
    dAVcl+;

%%%%%%%%%%%%%%%%%%%%
% Negator
%%%%%%%%%%%%%%%%%%%%

/th/words/words.neg.lhs :
    dAXg+;

/th/words/words.neg.rhs :
    dAVgr-;

%%%%%%%%%%%%%%%%%%%%
% Preposition
%%%%%%%%%%%%%%%%%%%%

<prep-noun> :
    ((dAVpr- or [dAJpr-]) & hPO+)
    or [hPO+ & <utterance-prep>]
    or (hPO+ & {PUsr+} & dAVpl+);

<prep-verb> :
    ((dAVpr- or [dAJpr-]) & hPC+)
    or [hPC+ & <utterance-prep>]
    or (hPC+ & {PUsr+} & dAVpl+);

<prep-lw-comp> :
    @hPO+
    or @hPC+
    or @hAVp+
    or @hAJp+;

<prep-lw> :
    ((dAVpr- or [dAJpr-]) & <prep-lw-comp>)
    or [<prep-lw-comp> & <utterance-prep>]
    or (<prep-lw-comp> & {PUsr+} & dAVpl+);

<nprep-noun> :
    (dAJpr- & hPO+)
    or [hPO+ & <utterance-prep>]
    or (hPO+ & {PUsr+} & dAVpl+);

<nprep-verb> :
    (dAJpr- & hPC+)
    or [hPC+ & <utterance-prep>]
    or (hPC+ & {PUsr+} & dAVpl+);

<vprep-noun> :
    (dAVpr- & hPO+)
    or [hPO+ & <utterance-prep>]
    or (hPO+ & {PUsr+} & dAVpl+);

<vprep-verb> :
    (dAVpr- & hPC+)
    or [hPC+ & <utterance-prep>]
    or (hPC+ & {PUsr+} & dAVpl+);

/th/words/words.prep.n :
    <prep-noun>;

/th/words/words.prep.v :
    <prep-verb>;

/th/words/words.prep.nv :
    <prep-noun>
    or <prep-verb>;

/th/words/words.prep-n.n :
    <nprep-noun>;

/th/words/words.prep-n.v :
    <nprep-verb>;

% /th/words/words.nprep.nv :
%     <nprep-noun>
%     or <nprep-verb>;

/th/words/words.prep-v.n :
    <vprep-noun>;

/th/words/words.prep-v.v :
    <vprep-verb>;

/th/words/words.prep-v.nv :
    <vprep-noun>
    or <vprep-verb>;

/th/words/words.prep.lw :
    <prep-lw>;

%%%%%%%%%%%%%%%%%%%%
% Relative Pronoun
%%%%%%%%%%%%%%%%%%%%

<relcls> :
    {hCLn-}
    & dAJr-
    & hPC+;

<relsent> :
    (dAVr- or hRC+)
    & <left-wall-sent>
    & <right-wall>;

/th/words/words.relpro.common :
    <relcls> or [<relsent>];

%%%%%%%%%%%%%%%%%%%%
% Conjunction
%%%%%%%%%%%%%%%%%%%%

/th/words/words.conj.noun :
    {JD-}
    & {PUs-}
    & {PUs+}
    & JNl-
    & JNr+
    & <noun-start>
    & <noun-element>;

_.cn :
    [[
        JNl-
        & JNr+
    ]]
    & <noun-start>
    & <noun-element>;

/th/words/words.conj.verb :
    {JD-}
    & {PUs-}
    & {PUs+}
    & JVl-
    & JVr+;

_.cv :
    [[
        JVl-
        & JVr+
    ]];

/th/words/words.conj.discont :
    JD+;

%%%%%%%%%%%%%%%%%%%%
% Particle
%%%%%%%%%%%%%%%%%%%%

/th/words/words.part.common :
    PT-;

%%%%%%%%%%%%%%%%%%%%
% Interjection
%%%%%%%%%%%%%%%%%%%%

/th/words/words.intj.common
EMOTICON :
    {PUs-}
    & {PUs+}
    & (IJ+ or PT-);

%%%%%%%%%%%%%%%%%%%%
% Punctuation
%%%%%%%%%%%%%%%%%%%%

_.pu :
    PUs+ or PUs-;

ๆ.pu :
    {PUs-}
    & dPUy-
    & {PUs+};

ฯ.pu :
    {PUs-}
    & dPUp-
    & {PUs+};

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

LEFT-WALL :
    LW+;

RIGHT-WALL :
    RW-;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

<UNKNOWN-WORD> : XXXXXXXX+;

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
